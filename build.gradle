buildscript {
    def env = { String name -> System.getProperty(name, System.getenv(name)) }

    ext.databaseHost = env("JDBC_HOST") ?: "122.49.74.102"
    ext.databasePort = env("JDBC_POST") ?: 3306
    ext.databaseUsername = env("JDBC_USERNAME") ?: "eicn"
    ext.databasePassword = env("JDBC_PASSWORD") ?: "eicnrw"

    ext.databaseName = env("JDBC_DATABASE") ?: "eicn"
    ext.databaseUrl = "jdbc:mysql://$databaseHost:$databasePort/$databaseName?useUnicode=yes&characterEncoding=utf8&autoReconnect=true&autoReconnectForPools=true&zeroDateTimeBehavior=convertToNull&useSSL=false"

    ext.profile = (!project.hasProperty('profile') || !profile) ? 'local' : profile

    ext {
        springBootVersion = '2.3.0.RELEASE'
        springCloudVersion = 'Hoxton.SR4'
        vavrVersion = '0.10.2'
        mysqlVersion = '5.1.47'
        jooqVersion = '3.13.2'
        poiVersion = '4.1.0'
        okhttpVersion = '4.2.2'
        //okhttpVersion = '3.12.0'
        retrofitVersion = '2.9.0'
        gsonVersion = '2.8.6'
        ckanJavaApiVersion = '1.2.0.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.20"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:1.0.9.RELEASE"
        classpath "io.freefair.gradle:lombok-plugin:5.0.1"
        classpath "nu.studer:gradle-jooq-plugin:4.2"
        classpath "gradle.plugin.org.flywaydb:gradle-plugin-publishing:6.4.2"
//        classpath files("gradle/lib/gradle-css-plugin-6.0.0-tinywind-custom.jar")
        classpath 'de.obqo.gradle:gradle-lesscss-plugin:1.0-1.3.3'
    }
}

plugins {
//    id "com.eriwen.gradle.css" version "2.14.0"
}

allprojects {
    group 'kr.co.eicn'
    version '1.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'idea'
    apply plugin: "io.freefair.lombok"
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"

    repositories {
        mavenCentral()
        maven {
            url 'https://jitpack.io'
        }
    }

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    sourceSets {
        main {
            resources {
                srcDirs "src/main/resources", "src/main/resources-${profile}"
            }
        }
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }

        implementation 'org.jetbrains.kotlin:kotlin-reflect'
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core'
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8'

        implementation 'io.springfox:springfox-swagger2:2.8.0'
        implementation 'io.springfox:springfox-swagger-ui:2.8.0'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
}

project(':util') {
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        compileOnly 'org.springframework:spring-core'
        compileOnly 'org.springframework:spring-web'
        compileOnly 'org.springframework:spring-webmvc'
        compileOnly 'org.springframework:spring-context'
        compileOnly 'org.springframework:spring-jdbc'
        compileOnly 'org.jooq:jooq'
        compileOnly 'javax.servlet:javax.servlet-api'
        compileOnly 'javax.validation:validation-api'

        api 'org.apache.commons:commons-lang3:3.8.1'
        api 'org.apache.httpcomponents:httpcore:4.4.12'
        api 'org.apache.httpcomponents:httpclient:4.5.10'
        api "io.vavr:vavr-jackson:${vavrVersion}"
        api "org.apache.poi:poi:${poiVersion}"
        api "org.apache.poi:poi-ooxml:${poiVersion}"
    }
}

project(':jooq-model') {
    bootJar.enabled = false
    jar.enabled = true

    apply plugin: 'nu.studer.jooq'
    jooq {
        version = jooqVersion
        edition = 'OSS'
        meta(sourceSets.main) {
            jdbc {
                driver = 'com.mysql.jdbc.Driver'
                url = databaseUrl
                user = databaseUsername
                password = databasePassword
            }
            generator {
                name = 'org.jooq.codegen.DefaultGenerator'
                strategy {
                    name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
                database {
                    name = 'org.jooq.meta.mysql.MySQLDatabase'
                    schemata {
                        schema {
                            inputSchema = 'eicn'
                        }
                        schema {
                            inputSchema = 'CUSTOMDB'
                        }
                        schema {
                            inputSchema = 'CONFIGDB'
                        }
                        schema {
                            inputSchema = 'STATDB'
                        }
                        schema {
                            inputSchema = 'PDS'
                        }
                    }

                    excludes = 'schema_version' +
                            '| menu_company_.*' +
                            '| cst_custom_info_.*' +
                            '| maindb_custom_info_.*' +
                            '| maindb_key_info_.*' +
                            '| maindb_multichannel_info_.*' +
                            '| result_custom_info_.*' +
                            '| talk_msg_.*' +
                            '| talk_room_.*' +
                            '| voc_result_.*' +
                            '| execute_pds_custom_info_.*' +
                            '| pds_custom_info_.*' +
                            '| result_custom_info_.*' +
                            '| stat_inbound_.*' +
                            '| stat_member_.*' +
                            '| stat_outbound_.*' +
                            '| stat_talk_.*' +
                            '| stat_user_inbound_.*' +
                            '| stat_user_outbound_.*' +
                            '| stat_voc_.*' +
                            '| stat_queue_wait_.*' +
                            '| chatt_bookmark_.*' +
                            '| chatt_msg_read_.*' +
                            '| chatt_room_member_.*' +
                            '| eicn_cdr_.*' +
                            '| history_queue_wait_.*' +
                            '| CUSTOMDB.member_status_.*' +
                            '| prv_custom_info_.*' +
                            '| voc_custom_list_.*' +
                            '| voc_research_result_.*' +
                            '| pds_research_result_.*' +
                            '| stat_user_ranking_.*'

                    forcedTypes {
                        forcedType {
                            name = "BOOLEAN"
                            expression = ".*"
                            types = "(?i:TINYINT)\\(1\\)"
                        }
                    }
                }
                generate {
                    relations = true
                    deprecated = false
                    records = true
                    pojos = true
                    fluentSetters = true
                    javaTimeTypes = false
                }
                target {
                    packageName = 'kr.co.eicn.ippbx.meta.jooq'
                }
            }
        }
    }

    dependencies {
        jooqRuntime "mysql:mysql-connector-java:${mysqlVersion}"
        api "mysql:mysql-connector-java:${mysqlVersion}"
        api 'org.jooq:jooq'
        api 'javax.persistence:javax.persistence-api:2.2'
    }
}

project(':model-extension') {
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        compile project(':util')
        compile project(':jooq-model')

        compileOnly 'javax.validation:validation-api'
        compileOnly 'org.springframework:spring-web'
        compileOnly 'org.springframework.security:spring-security-core'

        api 'commons-fileupload:commons-fileupload:1.3.2'
        api 'io.jsonwebtoken:jjwt:0.9.1'
    }
}

project(':api-server') {
    apply plugin: 'war'

    configurations {
        optional
        compile.extendsFrom optional
    }

    dependencies {
        compile project(':model-extension')

        compileOnly 'org.springframework.boot:spring-boot-starter-tomcat'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-aop'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-jdbc'
        implementation 'org.springframework.boot:spring-boot-starter-jooq'
        implementation "org.springframework.boot:spring-boot-starter-data-rest"
        implementation "org.springframework.boot:spring-boot-starter-mail"
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation "org.springframework.boot:spring-boot-starter-cache"
        implementation "org.springframework.boot:spring-boot-starter-batch"

        optional "org.springframework.boot:spring-boot-configuration-processor"

        implementation 'org.apache.tomcat.embed:tomcat-embed-jasper'
        implementation 'taglibs:standard:1.1.2'
        implementation 'javax.servlet:jstl'

        implementation "org.springframework.restdocs:spring-restdocs-mockmvc"
        implementation "org.mybatis.spring.boot:mybatis-spring-boot-starter:2.1.1"

        implementation 'org.modelmapper:modelmapper:2.3.5'
        implementation 'net.rakugakibox.util:yaml-resource-bundle:1.1'
        implementation 'commons-io:commons-io:2.5'
        implementation 'org.apache.commons:commons-collections4:4.4'
        implementation 'org.springdoc:springdoc-openapi-ui:1.2.16'

        implementation files('libs/diotts_class.jar')

        testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
        testImplementation 'org.springframework.security:spring-security-test'
    }

}

project(':front-controller') {
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        compile project(':model-extension')

        api 'org.springframework.boot:spring-boot-starter-web'
        api 'org.springframework.boot:spring-boot-starter-aop'
        api 'org.springframework.boot:spring-boot-starter-validation'
        api "org.springframework.boot:spring-boot-starter-data-rest"
        api "org.springframework.boot:spring-boot-starter-mail"
        api "org.springframework.boot:spring-boot-starter-cache"
        api "org.springframework.boot:spring-boot-starter-batch"
        api 'org.springframework.boot:spring-boot-starter-security'
        api "org.springframework.session:spring-session-core"

        api 'org.springframework.security:spring-security-core'
        api 'org.apache.tomcat.embed:tomcat-embed-jasper'
        api 'taglibs:standard:1.1.2'
        api 'javax.servlet:jstl'

        api 'com.ibm.icu:icu4j:67.1'

        testApi 'org.springframework.restdocs:spring-restdocs-mockmvc'
        testApi 'org.springframework.security:spring-security-test'
    }
}

project(':lgu-front') {
    apply plugin: 'war'

    apply plugin: 'lesscss'
    lesscss {
        source = fileTree('src/main/webapp/resources/less') {
            include 'app.less'
        }
        dest = 'src/main/webapp/resources/compiled'
        compress = true
    }
    war {
        dependsOn 'lesscss'
    }
    bootWar {
        dependsOn 'lesscss'
    }

    dependencies {
        compile project(':front-controller')
        compileOnly 'org.springframework.boot:spring-boot-starter-tomcat'

        implementation 'org.webjars:jquery:3.4.1'
        implementation 'org.webjars:jquery-blockui:2.65'
        implementation 'org.webjars:momentjs:2.21.0'
        implementation 'org.webjars.bower:less.js:2.7.3'
        implementation 'org.webjars:font-awesome:5.13.0'

        implementation 'org.webjars:jquery-ui:1.12.1'
        implementation 'org.webjars:d3js:5.9.1'
        implementation 'org.webjars:Semantic-UI:2.4.1'
        implementation 'org.webjars.npm:overlayscrollbars:1.9.1'
        implementation 'org.webjars:material-design-icons:3.0.1'
        implementation 'org.webjars:toastr:2.1.2'
        implementation 'org.webjars:cryptojs:3.1.2'

        implementation 'org.webjars:sockjs-client:1.0.2'
        implementation 'org.webjars:stomp-websocket:2.3.3'

        implementation 'org.webjars.npm:vue:3.2.10'
    }
}

project(':skt-front') {
    apply plugin: 'war'

    apply plugin: 'lesscss'
    lesscss {
        source = fileTree('src/main/webapp/resources/less') {
            include 'app.less'
        }
        dest = 'src/main/webapp/resources/compiled'
        compress = true
    }
    war {
        dependsOn 'lesscss'
    }
    bootWar {
        dependsOn 'lesscss'
    }

    dependencies {
        compile project(':front-controller')
        compileOnly 'org.springframework.boot:spring-boot-starter-tomcat'

        implementation 'org.webjars:jquery:3.4.1'
        implementation 'org.webjars:jquery-blockui:2.65'
        implementation 'org.webjars:momentjs:2.21.0'
        implementation 'org.webjars.bower:less.js:2.7.3'
        implementation 'org.webjars:font-awesome:5.13.0'

        implementation 'org.webjars:jquery-ui:1.12.1'
        implementation 'org.webjars:d3js:5.9.1'
        implementation 'org.webjars:Semantic-UI:2.4.1'
        implementation 'org.webjars.npm:overlayscrollbars:1.9.1'
        implementation 'org.webjars:material-design-icons:3.0.1'
        implementation 'org.webjars:toastr:2.1.2'
        implementation 'org.webjars:cryptojs:3.1.2'

        implementation 'org.webjars:sockjs-client:1.0.2'
        implementation 'org.webjars:stomp-websocket:2.3.3'
    }
}
